# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")

config("public_config") {
  include_dirs = [ "public" ]
  visibility = [ ":*" ]
}

config("overrides_config") {
  include_dirs = [ "public_overrides" ]
  visibility = [ ":*" ]
}

# This source set provides the <span> header, which is accessed only through
# pw_polyfill.
pw_source_set("polyfill") {
  remove_public_deps = [ "*" ]
  public_configs = [ ":overrides_config" ]
  public_deps = [ ":pw_span" ]
  public = [ "public_overrides/span" ]
  visibility = [ "$dir_pw_polyfill:*" ]
}

# This source set provides the internal span.h header included by <span>. This
# source set is only used by pw_polyfill, so its visibility is restricted.
pw_source_set("pw_span") {
  remove_public_deps = [ "*" ]
  public_configs = [ ":public_config" ]
  public_deps = [ "$dir_pw_polyfill" ]
  sources = [ "public/pw_span/internal/span.h" ]
  visibility = [ ":*" ]
}

pw_test_group("tests") {
  tests = [ ":test" ]
}

pw_test("test") {
  deps = [ ":pw_span" ]
  remove_configs = [ "$dir_pw_build:extra_strict_warnings" ]
  sources = [ "span_test.cc" ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}
