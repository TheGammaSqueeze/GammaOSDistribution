# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/facade.gni")
import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("backend.gni")

config("default_config") {
  include_dirs = [ "public" ]
}

config("backend_config") {
  include_dirs = [ "public_overrides" ]
}

pw_facade("handler") {
  backend = pw_assert_basic_HANDLER_BACKEND
  public_configs = [ ":default_config" ]
  public_deps = [ "$dir_pw_preprocessor" ]
  public = [ "public/pw_assert_basic/handler.h" ]
}

pw_source_set("pw_assert_basic") {
  public_configs = [
    ":backend_config",
    ":default_config",
  ]
  deps = [
    "$dir_pw_assert:facade",
    "$dir_pw_preprocessor",
    pw_assert_basic_HANDLER_BACKEND,
  ]
  public = [
    "public/pw_assert_basic/assert_basic.h",
    "public/pw_assert_basic/handler.h",
    "public_overrides/pw_assert_backend/assert_backend.h",
  ]
  sources = [ "assert_basic.cc" ]
}

# A basic handler backend using sysio.
pw_source_set("basic_handler") {
  deps = [
    ":handler.facade",
    "$dir_pw_assert:facade",
    "$dir_pw_preprocessor",
    "$dir_pw_string",
    "$dir_pw_sys_io",
  ]
  sources = [ "basic_handler.cc" ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}
