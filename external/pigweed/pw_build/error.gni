# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("python_action.gni")

# Prints an error message and exits the build unsuccessfully. Either 'message'
# or 'message_lines' must be specified, but not both.
#
# Args:
#   message: The message to print. Use \n for newlines.
#   message_lines: List of lines to use for the message.
#
template("pw_error") {
  assert(
      defined(invoker.message) != defined(invoker.message_lines),
      "pw_error requires either a 'message' string or a 'message_lines' list")

  if (defined(invoker.message_lines)) {
    _message = string_join("\n", invoker.message_lines)
  } else {
    _message = invoker.message
  }
  assert(_message != "", "The message cannot be empty")

  action(target_name) {
    script = "$dir_pw_build/py/pw_build/error.py"
    args = [
      "--target",
      get_label_info(":$target_name", "label_with_toolchain"),
      "--message",
      _message,
      "--root",
      rebase_path("//"),
      "--out",
      rebase_path(root_build_dir),
    ]

    # This output file is never created.
    outputs = [ "$target_gen_dir/$target_name.build_error" ]
  }
}
