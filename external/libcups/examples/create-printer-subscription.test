# Create a printer subscription.
#
# Usage:
#
#   ./ipptool [-d recipient=uri] printer-uri create-printer-subscription.test
{
	# The name of the test...
	NAME "Create a push printer subscription"
	SKIP-IF-NOT-DEFINED recipient

	# The operation to use
	OPERATION Create-Printer-Subscription

	# The attributes to send
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri

	GROUP subscription-attributes-tag
	ATTR uri notify-recipient-uri $recipient
	ATTR keyword notify-events printer-config-changed,printer-state-changed

	# What statuses are OK?
	STATUS successful-ok

	# What attributes do we expect?
	EXPECT notify-subscription-id OF-TYPE integer WITH-VALUE >0
	DISPLAY notify-subscription-id
}

{
	# The name of the test...
	NAME "Create a pull printer subscription"
	SKIP-IF-DEFINED recipient

	# The operation to use
	OPERATION Create-Printer-Subscription

	# The attributes to send
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR language attributes-natural-language en
	ATTR uri printer-uri $uri

        GROUP subscription-attributes-tag
	ATTR keyword notify-pull-method ippget
	ATTR keyword notify-events printer-config-changed,printer-state-changed

	# What statuses are OK?
	STATUS successful-ok

	# What attributes do we expect?
	EXPECT notify-subscription-id OF-TYPE integer WITH-VALUE >0
	DISPLAY notify-subscription-id
}
